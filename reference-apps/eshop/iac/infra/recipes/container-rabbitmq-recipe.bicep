@description('Information about what resource is calling this Recipe. Generated by Radius. For more information visit https://docs.radapp.dev/operations/custom-recipes/')
param context object

@description('Name of the queue. Defaults to the name of the Radius RabbitMQ resource.')
param queue string = context.resource.name

@description('Tag to pull for the rabbitmq container image.')
param tag string = '3'

@description('Memory request for the rabbitmq deployment.')
param memoryRequest string = '256Mi'

@description('Memory limit for the rabbitmq deployment')
param memoryLimit string = '1024Mi'

import kubernetes as kubernetes {
  kubeConfig: ''
  namespace: context.runtime.kubernetes.namespace
}

var uniqueName = 'rabbitmq-${uniqueString(context.resource.id)}'
var port = 5672

resource rabbitmq 'apps/Deployment@v1' = {
  metadata: {
    name: uniqueName
  }
  spec: {
    selector: {
      matchLabels: {
        app: 'rabbitmq'
        resource: context.resource.name
      }
    }
    template: {
      metadata: {
        labels: {
          app: 'rabbitmq'
          resource: context.resource.name
          // Label pods with the application name so `rad run` can find the logs.
          'radius.dev/application': context.application == null ? '' : context.application.name
        }
      }
      spec: {
        containers: [
          {
            name: 'rabbitmq'
            image: 'rabbitmq:${tag}'
            ports: [
              {
                containerPort: port
              }
            ]
            resources: {
              requests: {
                memory: memoryRequest
              }
              limits: {
                memory: memoryLimit
              }
            }
            env: []
          }
        ]
      }
    }
  }
}

resource svc 'core/Service@v1' = {
  metadata: {
    name: uniqueName
    labels: {
      name: uniqueName
    }
  }
  spec: {
    type: 'ClusterIP'
    selector: {
      app: 'rabbitmq'
      resource: context.resource.name
    }
    ports: [
      {
        port: port
      }
    ]
  }
}

output result object = {
  resources: [
    '/planes/kubernetes/local/namespaces/${svc.metadata.namespace}/providers/core/Service/${svc.metadata.name}'
    '/planes/kubernetes/local/namespaces/${rabbitmq.metadata.namespace}/providers/apps/Deployment/${rabbitmq.metadata.name}'
  ]
  values: {
    queue: queue
  }
  secrets: {
    connectionString: '${svc.metadata.name}.${svc.metadata.namespace}.svc.cluster.local'
  }
}
